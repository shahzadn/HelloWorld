apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.iqvis.helloworld"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

checkstyle {
    toolVersion = '7.6'
    ignoreFailures = true
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

findbugs {
    toolVersion '3.0.1'
    ignoreFailures = true
}

pmd {
    toolVersion '5.5.4'
    ignoreFailures = true
    ruleSetFiles = files("$project.rootDir/config/pmd/ruleset.xml")
    rulePriority = 3
}

task customCheckstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    reports {
        xml.enabled = true
        xml.destination = "$project.buildDir/reports/checkstyle/checkstyle.xml"
        html.enabled = true
        html.destination = "$project.buildDir/reports/checkstyle/checkstyle.html"
    }
}

task customFindbugs(type: FindBugs) {
    effort = "default"
    reportLevel = "medium"
    excludeFilter = new File("${project.rootDir}/config/findbugs/filters.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")
    source = fileTree('src/main/java/')
    classpath = files()

    reports {
        xml.enabled = true
        html.enabled = false
        xml.destination = "$project.buildDir/reports/findbugs/findbugs.xml"

    }
}

task customPmd(type: Pmd) {
    source = fileTree('src/main/java/')

    reports {
        xml.enabled = true
        xml.destination = "$project.buildDir/reports/pmd/pmd.xml"
        html.enabled = true
        html.destination = "$project.buildDir/reports/pmd/pmd.html"
    }
}

build.dependsOn customCheckstyle, customFindbugs, customPmd


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'testfairy:testfairy-android-sdk:1.4.16@aar'
}
